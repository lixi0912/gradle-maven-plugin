apply plugin: 'maven-publish'

def mavHost = project.properties.getOrDefault('mavHost', 'https://raw.githubusercontent.com/sky-uk/gradle-maven-plugin/master/')
apply from: "${mavHost}/utils.gradle"
apply from: "${mavHost}/artifact-javadoc-handler.gradle"
apply from: "${mavHost}/artifact-pom-manager.gradle"

def getProjectVersion() {
    def versionName
    if (isAndroidProject()) {
        versionName = getStringProperty("mavVersion", project.android.defaultConfig.versionName)
    } else {
        versionName = getStringProperty("mavVersion", project.version)
    }
    if (getBooleanProperty("mavSnapshot")) {
        versionName += "-SNAPSHOT"
    }
    return versionName
}

project.afterEvaluate {
    publishing {
        publications {
            mavenPublish(MavenPublication) {
                groupId getStringProperty('mavGroupId')
                artifactId getStringProperty("mavProjectName", project.name)
                version = getProjectVersion()

                if (isAndroidProject()) {
                    artifact bundleReleaseAar
                    artifact androidJavadocsJar
                    artifact androidSourcesJar
                } else {
                    artifact jar
                    artifact sourcesJar
                    artifact javadocJar
                }

                decoratePom(pom)
            }
        }

        repositories {
            def isToRemoteRepo = getBooleanProperty("mavPublishToRemoteRepo")
            def isToInternalRepo = getBooleanProperty("mavPublishToInternalRepo")
            def isToMavenLocal = getBooleanProperty("mavPublishToMavenLocal")

            if (isToRemoteRepo) {
                maven {
                    credentials {
                        username getStringProperty("mavRemoteRepoUser")
                        password getStringProperty("mavRemoteRepoPassword")
                    }
                    url = getStringProperty("mavRepoRemoteUrl")
                }
            }

            if (isToInternalRepo) {
                maven {
                    url = getStringProperty("mavRepoInternalUrl")
                }
            }

            if (isToMavenLocal || (!isToInternalRepo && !isToRemoteRepo)) {
                mavenLocal()
            }
        }
    }
}
